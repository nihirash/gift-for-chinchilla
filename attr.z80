; DE - COORDS
; HL - Addr
xyToAttr:
    ld h, 0 : ld l, d
    dup 5
    add hl, hl
    edup 
    ld d, #58
    add hl, de 
    ret

attrCharColor db %01101101

; DE - coords
; HL - string
printAttr:
    ld a, (hl)
    or a : ret z
    push hl : push de
    call putAttrC
    pop de 
    ld hl, 5
    add hl, de
    ex hl, de
    pop hl
    inc hl
    jr printAttr

; DE - coords
; A - char
putAttrC:
    push de
    ld l, a
    ld h,0
    dup 3
    add hl, hl
    edup
    ld de, font
    add hl, de
    ld bc, hl
    pop de 
    call xyToAttr
    ex hl, de
    ld hl, bc
    ld b, 8
.loop
    ld a, (hl)
    dup 4
    rla 
    push af
    ld a, 1 
    jr nc, 1F
    ld a, (attrCharColor)
1   ld (de), a
    pop af 
    inc de
    edup
    push hl
    ld hl, 28
    add hl, de
    ex hl, de
    pop hl
    inc hl
    djnz .loop
    ret
